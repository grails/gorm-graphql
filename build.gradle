buildscript {
    repositories {
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsGradlePluginVersion"
        classpath "org.grails.plugins:views-gradle:$viewGradleVersion"
        classpath "org.grails.plugins:views-json:$viewsJsonVersion"
        classpath "org.grails:grails-docs:${project.ext.properties.grailsDocsVersion ?: grailsVersion}"
        classpath "io.github.gradle-nexus:publish-plugin:1.3.0"
    }
}

repositories {
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
}

version project.projectVersion

ext {
    commonBuild = 'https://raw.githubusercontent.com/grails/grails-common-build/v2.0.1'
}

subprojects {

    version project.projectVersion

    ext {
        userOrg = "grails"
        isGrailsPlugin = name.startsWith('grails-plugin')
        isBuildSnapshot = version.toString().endsWith("-SNAPSHOT")
    }

    repositories {
        maven { url "https://repo.grails.org/grails/core" }
    }

    tasks.withType(GroovyCompile).configureEach {
        configure(groovyOptions) {
            forkOptions.jvmArgs = ['-Xmx1024m']
        }
    }

    tasks.withType(Test).configureEach {
        testLogging {
            events "failed"
            exceptionFormat "full"
            showStandardStreams true
        }
    }

    if (project.name.startsWith("examples-")) {
        if (project.name.startsWith("examples-grails-")) {
            apply plugin: "org.grails.grails-web"
        }
        return
    }

    if (isGrailsPlugin) {
        group "org.grails.plugins"
    } else {
        group "org.grails"
    }

    if (isGrailsPlugin) {
        apply plugin: 'groovy'
        apply plugin: 'eclipse'
        apply plugin: 'idea'
        apply plugin: 'java-library'
        apply plugin: "org.grails.grails-plugin"

        sourceCompatibility = 1.11
        targetCompatibility = 1.11
    } else {
        apply from: "${commonBuild}/common-project.gradle"
    }

    dependencies {
        implementation "com.graphql-java:graphql-java:$graphqlJavaVersion"
        testImplementation "org.codehaus.groovy:groovy-test:$groovyVersion"
        testImplementation "io.projectreactor:reactor-test:3.6.1"
        testImplementation("org.spockframework:spock-core:$spockVersion")
        implementation 'org.grails:grails-datastore-gorm:7.3.4'
    }
}

apply from: "${commonBuild}/common-publishing.gradle"
